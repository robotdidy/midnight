// SPDX-License-Identifier: GPL-2.0-or-later
// Copyright (c) 2025 Morpho Association
pragma solidity ^0.8.0;

import {Obligation, Offer, Signature, CollateralParams} from "../src/interfaces/IMidnight.sol";
import {Midnight} from "../src/Midnight.sol";
import {WAD, CALLBACK_SUCCESS} from "../src/libraries/ConstantsLib.sol";
import {UtilsLib} from "../src/libraries/UtilsLib.sol";
import {TickLib, MAX_TICK} from "../src/libraries/TickLib.sol";
import {ICallbacks} from "../src/interfaces/ICallbacks.sol";
import {IdLib} from "../src/libraries/IdLib.sol";

import {BaseTest} from "./BaseTest.sol";
import {ERC20} from "./erc20s/ERC20.sol";
import {Oracle} from "./helpers/Oracle.sol";

contract TakeTest is BaseTest {
    using UtilsLib for uint256;

    Obligation internal obligation;
    bytes32 internal id;
    Offer internal lenderOffer;
    Offer internal borrowerOffer;
    Offer internal otherLenderOffer;
    Offer internal otherBorrowerOffer;

    uint256 internal maxAssets = 1e33; // to refine.

    function setUp() public override {
        super.setUp();

        obligation.loanToken = address(loanToken);
        obligation.maturity = block.timestamp + 100;
        obligation.collateralParams
            .push(
                CollateralParams({
                    token: address(collateralToken1),
                    lltv: 0.77e18,
                    maxLif: maxLif(0.77e18, 0.25e18),
                    oracle: address(oracle1)
                })
            );
        obligation.collateralParams
            .push(
                CollateralParams({
                    token: address(collateralToken2),
                    lltv: 0.77e18,
                    maxLif: maxLif(0.77e18, 0.25e18),
                    oracle: address(oracle2)
                })
            );
        obligation.collateralParams = sortCollateralParams(obligation.collateralParams);
        obligation.rcfThreshold = 0;

        id = toId(obligation);

        lenderOffer.buy = true;
        lenderOffer.maker = lender;
        lenderOffer.maxUnits = type(uint256).max;
        lenderOffer.obligation = obligation;
        lenderOffer.expiry = block.timestamp + 200;
        lenderOffer.tick = MAX_TICK;

        otherLenderOffer.buy = false;
        otherLenderOffer.maker = otherLender;
        otherLenderOffer.receiverIfMakerIsSeller = otherLender;
        otherLenderOffer.maxUnits = type(uint256).max;
        otherLenderOffer.obligation = obligation;
        otherLenderOffer.expiry = block.timestamp + 200;
        otherLenderOffer.tick = MAX_TICK;

        borrowerOffer.buy = false;
        borrowerOffer.maker = borrower;
        borrowerOffer.receiverIfMakerIsSeller = borrower;
        borrowerOffer.maxUnits = type(uint256).max;
        borrowerOffer.obligation = obligation;
        borrowerOffer.expiry = block.timestamp + 200;
        borrowerOffer.tick = MAX_TICK;

        otherBorrowerOffer.buy = true;
        otherBorrowerOffer.maker = otherBorrower;
        otherBorrowerOffer.maxUnits = type(uint256).max;
        otherBorrowerOffer.obligation = obligation;
        otherBorrowerOffer.expiry = block.timestamp + 200;
        otherBorrowerOffer.tick = MAX_TICK;
    }

    // tests.

    // path 1: Lender enters + borrower enters.

    function testBuy1(uint256 units, uint256 tick) public {
        units = bound(units, 1, maxAssets);
        tick = bound(tick, 0, MAX_TICK);
        uint256 price = TickLib.tickToPrice(tick);
        vm.assume(price > 0.01 ether);
        borrowerOffer.tick = tick;
        uint256 expectedAssets = units.mulDivUp(price, WAD);
        deal(address(loanToken), lender, expectedAssets);
        collateralize(obligation, borrower, units);

        take(units, lender, borrowerOffer);

        assertEq(midnight.creditOf(id, lender), units, "lender units");
        assertEq(midnight.debtOf(id, borrower), units, "borrower debt");
        assertEq(midnight.totalUnits(id), units, "total units");
        assertEq(loanToken.balanceOf(borrower), expectedAssets, "borrower balance");
        assertEq(loanToken.balanceOf(lender), 0, "lender balance");
        assertEq(midnight.consumed(borrower, borrowerOffer.group), units, "consumed");
    }

    function testSell1(uint256 units, uint256 tick) public {
        units = bound(units, 1, maxAssets);
        tick = bound(tick, 0, MAX_TICK);
        uint256 price = TickLib.tickToPrice(tick);
        vm.assume(price > 0.01 ether);
        lenderOffer.tick = tick;
        uint256 expectedAssets = units.mulDivDown(price, WAD);
        deal(address(loanToken), lender, expectedAssets);
        collateralize(obligation, borrower, units);

        take(units, borrower, lenderOffer);

        assertEq(midnight.creditOf(id, lender), units, "lender units");
        assertEq(midnight.debtOf(id, borrower), units, "borrower debt");
        assertEq(midnight.totalUnits(id), units, "total units");
        assertEq(loanToken.balanceOf(borrower), expectedAssets, "borrower balance");
        assertEq(loanToken.balanceOf(lender), 0, "lender balance");
        assertEq(midnight.consumed(lender, lenderOffer.group), units, "consumed");
    }

    // path 2: Lender enters + lender exits.

    function testBuy2(uint256 units, uint256 tick, uint256 otherLenderUnits) public {
        units = bound(units, 0, maxAssets);
        tick = bound(tick, 0, 600);
        uint256 price = TickLib.tickToPrice(tick);
        vm.assume(price > 0.01 ether);
        uint256 buyerAssets = units.mulDivDown(price, WAD);
        otherLenderUnits = bound(otherLenderUnits, units, max(units, maxAssets));
        setupOtherUsers(obligation, otherLenderUnits);
        uint256 actualOtherLenderCredit = midnight.creditOf(id, otherLender);
        deal(address(loanToken), lender, buyerAssets + 1);
        otherLenderOffer.buy = false;
        otherLenderOffer.maxUnits = type(uint256).max;
        otherLenderOffer.tick = tick;
        uint256 totalUnitsBefore = midnight.totalUnits(id);

        take(units, lender, otherLenderOffer);

        assertEq(midnight.creditOf(id, lender), units, "lender units");
        assertEq(midnight.debtOf(id, lender), 0, "lender debt");
        assertEq(midnight.creditOf(id, otherLender), actualOtherLenderCredit - units, "other lender units");
        assertEq(midnight.debtOf(id, otherLender), 0, "other lender debt");
        assertEq(midnight.totalUnits(id), totalUnitsBefore, "total units");
    }

    function testSell2(uint256 units, uint256 tick, uint256 otherLenderUnits) public {
        units = bound(units, 0, maxAssets);
        tick = bound(tick, 0, 600);
        uint256 price = TickLib.tickToPrice(tick);
        vm.assume(price > 0.01 ether);
        uint256 buyerAssets = units.mulDivDown(price, WAD);
        otherLenderUnits = bound(otherLenderUnits, units, max(units, maxAssets));
        setupOtherUsers(obligation, otherLenderUnits);
        uint256 actualOtherLenderCredit = midnight.creditOf(id, otherLender);
        deal(address(loanToken), lender, buyerAssets + 1);
        lenderOffer.maxUnits = type(uint256).max;
        lenderOffer.tick = tick;
        uint256 totalUnitsBefore = midnight.totalUnits(id);

        take(units, otherLender, lenderOffer);

        assertEq(midnight.creditOf(id, lender), units, "lender units");
        assertEq(midnight.debtOf(id, lender), 0, "lender debt");
        assertEq(midnight.creditOf(id, otherLender), actualOtherLenderCredit - units, "other lender units");
        assertEq(midnight.debtOf(id, otherLender), 0, "other lender debt");
        assertEq(midnight.totalUnits(id), totalUnitsBefore, "total units");
    }

    // Lender sells more than their balance, crossing to borrower.
    function testCrossTopDown(uint256 units, uint256 otherLenderUnits) public {
        otherLenderUnits = bound(otherLenderUnits, 1, maxAssets - 1);
        units = bound(units, otherLenderUnits + 1, maxAssets);
        setupOtherUsers(obligation, otherLenderUnits);
        uint256 otherLenderCredit = midnight.creditOf(id, otherLender);
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        deal(address(loanToken), lender, units.mulDivUp(price, WAD));
        collateralize(obligation, otherLender, units);
        otherLenderOffer.tick = MAX_TICK;
        uint256 totalUnitsBefore = midnight.totalUnits(id);

        take(units, lender, otherLenderOffer);

        // otherLender crossed from lender to borrower.
        assertEq(midnight.creditOf(id, otherLender), 0, "otherLender credit");
        assertEq(midnight.debtOf(id, otherLender), units - otherLenderCredit, "otherLender debt");
        assertEq(midnight.creditOf(id, lender), units, "lender credit");
        assertEq(midnight.totalUnits(id), totalUnitsBefore + units - otherLenderCredit, "total units");
    }

    // path 3: Borrower exits + borrower enters.

    function testBuy3(uint256 units, uint256 tick, uint256 existingUnits) public {
        units = bound(units, 0, maxAssets);
        tick = bound(tick, 0, 600);
        existingUnits = bound(existingUnits, units, max(units, maxAssets));
        setupOtherUsers(obligation, existingUnits);
        uint256 otherBorrowerDebt = midnight.debtOf(id, otherBorrower);
        collateralize(obligation, borrower, units);
        borrowerOffer.maxUnits = type(uint256).max;
        borrowerOffer.tick = tick;
        uint256 price = TickLib.tickToPrice(tick);
        deal(address(loanToken), otherBorrower, units.mulDivUp(price, WAD));
        uint256 totalUnitsBefore = midnight.totalUnits(id);

        take(units, otherBorrower, borrowerOffer);

        assertEq(midnight.debtOf(id, borrower), units, "borrower debt");
        assertEq(midnight.debtOf(id, otherBorrower), otherBorrowerDebt - units, "otherBorrower debt");
        assertEq(midnight.totalUnits(id), totalUnitsBefore, "total units");
    }

    function testSell3(uint256 units, uint256 tick, uint256 existingUnits) public {
        units = bound(units, 0, maxAssets);
        tick = bound(tick, 0, 600);
        existingUnits = bound(existingUnits, units, max(units, maxAssets));
        setupOtherUsers(obligation, existingUnits);
        uint256 otherBorrowerDebt = midnight.debtOf(id, otherBorrower);
        collateralize(obligation, borrower, units);
        otherBorrowerOffer.maxUnits = type(uint256).max;
        otherBorrowerOffer.tick = tick;
        uint256 totalUnitsBefore = midnight.totalUnits(id);

        take(units, borrower, otherBorrowerOffer);

        assertEq(midnight.debtOf(id, borrower), units, "borrower debt");
        assertEq(midnight.debtOf(id, otherBorrower), otherBorrowerDebt - units, "otherBorrower debt");
        assertEq(midnight.totalUnits(id), totalUnitsBefore, "total units");
    }

    // Borrower buys more than their debt, crossing to lender.
    function testCrossBottomUp(uint256 units, uint256 otherUnits) public {
        otherUnits = bound(otherUnits, 1, maxAssets - 1);
        units = bound(units, otherUnits + 1, maxAssets);
        setupOtherUsers(obligation, otherUnits);
        uint256 otherBorrowerDebt = midnight.debtOf(id, otherBorrower);
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        deal(address(loanToken), otherBorrower, units.mulDivUp(price, WAD));
        collateralize(obligation, borrower, units);
        borrowerOffer.tick = MAX_TICK;
        uint256 totalUnitsBefore = midnight.totalUnits(id);

        take(units, otherBorrower, borrowerOffer);

        // otherBorrower crossed from borrower to lender.
        assertEq(midnight.debtOf(id, otherBorrower), 0, "otherBorrower debt");
        assertEq(midnight.creditOf(id, otherBorrower), units - otherBorrowerDebt, "otherBorrower credit");
        assertEq(midnight.debtOf(id, borrower), units, "borrower debt");
        assertEq(midnight.totalUnits(id), totalUnitsBefore + units - otherBorrowerDebt, "total units");
    }

    // path 4: Borrower exits + lender exits.

    function testBuy4(uint256 units, uint256 tick, uint256 existingUnits) public {
        units = bound(units, 0, maxAssets);
        tick = bound(tick, 0, MAX_TICK);
        uint256 price = TickLib.tickToPrice(tick);
        vm.assume(price > 0.01 ether);
        uint256 buyerAssets = units.mulDivUp(price, WAD);
        existingUnits = bound(existingUnits, units, max(units, maxAssets));
        setupOtherUsers(obligation, existingUnits);
        uint256 otherLenderCredit = midnight.creditOf(id, otherLender);
        uint256 otherBorrowerDebt = midnight.debtOf(id, otherBorrower);

        otherLenderOffer.maxUnits = type(uint256).max;
        otherLenderOffer.tick = tick;
        deal(address(loanToken), otherBorrower, buyerAssets);

        take(units, otherBorrower, otherLenderOffer);

        assertEq(midnight.creditOf(id, otherLender), otherLenderCredit - units, "otherLender units");
        assertEq(midnight.debtOf(id, otherBorrower), otherBorrowerDebt - units, "otherBorrower debt");
        assertEq(midnight.totalUnits(id), otherBorrowerDebt - units, "total units");
        assertEq(loanToken.balanceOf(otherLender), buyerAssets, "otherLender balance");
    }

    function testSell4(uint256 units, uint256 tick, uint256 existingUnits) public {
        units = bound(units, 0, maxAssets);
        tick = bound(tick, 0, MAX_TICK);
        uint256 price = TickLib.tickToPrice(tick);
        vm.assume(price > 0.01 ether);
        uint256 buyerAssets = units.mulDivDown(price, WAD);
        existingUnits = bound(existingUnits, units, max(units, maxAssets));
        setupOtherUsers(obligation, existingUnits);
        uint256 otherLenderCredit = midnight.creditOf(id, otherLender);
        uint256 otherBorrowerDebt = midnight.debtOf(id, otherBorrower);

        otherBorrowerOffer.maxUnits = type(uint256).max;
        otherBorrowerOffer.tick = tick;

        take(units, otherLender, otherBorrowerOffer);

        assertEq(midnight.creditOf(id, otherLender), otherLenderCredit - units, "otherLender units");
        assertEq(midnight.debtOf(id, otherBorrower), otherBorrowerDebt - units, "otherBorrower debt");
        assertEq(midnight.totalUnits(id), otherBorrowerDebt - units, "total units");
        assertEq(loanToken.balanceOf(otherLender), buyerAssets, "otherLender balance");
    }

    // reduceOnly tests.

    function testReduceOnlyBuySuccess(uint256 existingUnits, uint256 exitUnits) public {
        existingUnits = bound(existingUnits, 1, maxAssets);
        exitUnits = bound(exitUnits, 1, existingUnits);
        setupOtherUsers(obligation, existingUnits);

        otherBorrowerOffer.maxUnits = exitUnits;
        otherBorrowerOffer.reduceOnly = true;

        uint256 price = TickLib.tickToPrice(MAX_TICK);
        deal(address(loanToken), otherBorrower, exitUnits.mulDivUp(price, WAD));
        collateralize(obligation, borrower, exitUnits);

        uint256 debtBefore = midnight.debtOf(id, otherBorrower);
        uint256 totalUnitsBefore = midnight.totalUnits(id);

        take(exitUnits, borrower, otherBorrowerOffer);

        assertEq(midnight.debtOf(id, borrower), exitUnits, "borrower debt");
        assertEq(midnight.creditOf(id, otherBorrower), 0, "otherBorrower units");
        assertEq(midnight.debtOf(id, otherBorrower), debtBefore - exitUnits, "otherBorrower debt");
        assertEq(midnight.totalUnits(id), totalUnitsBefore, "total units");
    }

    function testReduceOnlyBuyRevert(uint256 existingUnits, uint256 exitUnits) public {
        existingUnits = bound(existingUnits, 1, maxAssets - 1);
        exitUnits = bound(exitUnits, existingUnits + 1, maxAssets);
        setupOtherUsers(obligation, existingUnits);

        otherBorrowerOffer.maxUnits = exitUnits;
        otherBorrowerOffer.reduceOnly = true;

        vm.expectRevert("maker credit or debt increased");
        take(exitUnits, borrower, otherBorrowerOffer);
    }

    function testReduceOnlySellSuccess(uint256 existingUnits, uint256 exitUnits) public {
        existingUnits = bound(existingUnits, 1, maxAssets);
        exitUnits = bound(exitUnits, 1, existingUnits);
        setupOtherUsers(obligation, existingUnits);

        otherLenderOffer.maxUnits = exitUnits;
        otherLenderOffer.reduceOnly = true;

        uint256 price = TickLib.tickToPrice(MAX_TICK);
        deal(address(loanToken), lender, exitUnits.mulDivUp(price, WAD));

        uint256 creditBefore = midnight.creditOf(id, otherLender);
        uint256 totalUnitsBefore = midnight.totalUnits(id);

        take(exitUnits, lender, otherLenderOffer);

        assertEq(midnight.creditOf(id, lender), exitUnits, "lender units");
        assertEq(midnight.debtOf(id, lender), 0, "lender debt");
        assertEq(midnight.creditOf(id, otherLender), creditBefore - exitUnits, "other lender units");
        assertEq(midnight.debtOf(id, otherLender), 0, "other lender debt");
        assertEq(midnight.totalUnits(id), totalUnitsBefore, "total units");
    }

    function testReduceOnlySellRevert(uint256 existingUnits, uint256 exitUnits) public {
        existingUnits = bound(existingUnits, 1, maxAssets - 1);
        exitUnits = bound(exitUnits, existingUnits + 1, maxAssets);
        setupOtherUsers(obligation, existingUnits);

        otherLenderOffer.maxUnits = exitUnits;
        otherLenderOffer.reduceOnly = true;

        vm.expectRevert("maker credit or debt increased");
        take(exitUnits, lender, otherLenderOffer);
    }

    // group tests.

    function testBuyConsumed(uint256 units, uint256 offerUnits, uint256 secondRevertingTake, uint256 secondPassingTake)
        public
    {
        units = bound(units, 0, maxAssets - 1);
        offerUnits = bound(offerUnits, units, maxAssets - 1);
        secondRevertingTake = bound(secondRevertingTake, offerUnits - units + 1, maxAssets);
        secondPassingTake = bound(secondPassingTake, 0, offerUnits - units);
        borrowerOffer.maxUnits = offerUnits;
        borrowerOffer.tick = MAX_TICK;
        deal(address(loanToken), lender, offerUnits);
        collateralize(obligation, borrower, offerUnits);

        take(units, lender, borrowerOffer);

        vm.expectRevert("consumed");
        take(secondRevertingTake, lender, borrowerOffer);

        take(secondPassingTake, lender, borrowerOffer);
    }

    function testSellConsumed(uint256 units, uint256 offerUnits, uint256 secondRevertingTake, uint256 secondPassingTake)
        public
    {
        units = bound(units, 0, maxAssets - 1);
        offerUnits = bound(offerUnits, units, maxAssets - 1);
        secondRevertingTake = bound(secondRevertingTake, offerUnits - units + 1, maxAssets);
        secondPassingTake = bound(secondPassingTake, 0, offerUnits - units);
        lenderOffer.maxUnits = offerUnits;
        lenderOffer.tick = MAX_TICK;
        deal(address(loanToken), lender, offerUnits);
        collateralize(obligation, borrower, offerUnits);

        take(units, borrower, lenderOffer);

        vm.expectRevert("consumed");
        take(secondRevertingTake, borrower, lenderOffer);

        take(secondPassingTake, borrower, lenderOffer);
    }

    function testBuyGroup(uint256 firstFill, uint256 secondFill) public {
        firstFill = bound(firstFill, 0, maxAssets);
        secondFill = bound(secondFill, 0, maxAssets);
        borrowerOffer.maxUnits = firstFill + secondFill;
        borrowerOffer.tick = MAX_TICK;
        Offer memory borrowerOffer2 = borrowerOffer;
        borrowerOffer2.obligation.maturity = obligation.maturity + 100;
        deal(address(loanToken), lender, firstFill + secondFill);
        collateralize(obligation, borrower, firstFill);
        collateralize(borrowerOffer2.obligation, borrower, secondFill);

        take(firstFill, lender, borrowerOffer);

        vm.expectRevert("consumed");
        take(secondFill + 1, lender, borrowerOffer2);

        take(secondFill, lender, borrowerOffer2);
    }

    function testSellGroup(uint256 firstFill, uint256 secondFill) public {
        firstFill = bound(firstFill, 0, maxAssets);
        secondFill = bound(secondFill, 0, maxAssets);
        lenderOffer.maxUnits = firstFill + secondFill;
        lenderOffer.tick = MAX_TICK;
        Offer memory lenderOffer2 = lenderOffer;
        lenderOffer2.obligation.maturity = obligation.maturity + 100;
        deal(address(loanToken), lender, firstFill + secondFill);
        collateralize(obligation, borrower, firstFill);
        collateralize(lenderOffer2.obligation, borrower, secondFill);

        take(firstFill, borrower, lenderOffer);

        vm.expectRevert("consumed");
        take(secondFill + 1, borrower, lenderOffer2);

        take(secondFill, borrower, lenderOffer2);
    }

    // other tests.

    // address(this) makes an arbitrage for 2 crossed offers.
    function testMatch(uint256 units, uint256 tick1, uint256 tick2) public {
        units = bound(units, 1, maxAssets);
        tick1 = bound(tick1, 600, MAX_TICK);
        tick2 = bound(tick2, 600, MAX_TICK);
        uint256 price1 = TickLib.tickToPrice(tick1);
        uint256 price2 = TickLib.tickToPrice(tick2);
        vm.assume(price1 > price2);
        vm.assume(price1 > 0.5 ether);
        vm.assume(price2 > 0.5 ether);
        borrowerOffer.maxUnits = units;
        borrowerOffer.tick = tick1;
        lenderOffer.maxUnits = units;
        lenderOffer.tick = tick2;

        deal(address(loanToken), lender, units.mulDivDown(price2, WAD));
        deal(address(loanToken), address(this), units.mulDivUp(price1, WAD));
        collateralize(obligation, borrower, units);

        take(units, address(this), borrowerOffer);
        take(units, address(this), lenderOffer);

        assertEq(midnight.creditOf(id, address(this)), 0, "credit");
        assertEq(midnight.debtOf(id, address(this)), 0, "debt");
    }

    // address(this) makes an arbitrage for 2 crossed offers.
    function testMatchInverse(uint256 units, uint256 tick1, uint256 tick2) public {
        units = bound(units, 1, maxAssets);
        tick1 = bound(tick1, 600, MAX_TICK);
        tick2 = bound(tick2, 600, MAX_TICK);
        uint256 price1 = TickLib.tickToPrice(tick1);
        uint256 price2 = TickLib.tickToPrice(tick2);
        vm.assume(price2 > price1);
        vm.assume(price1 > 0.5 ether);
        vm.assume(price2 > 0.5 ether);
        borrowerOffer.maxUnits = units;
        borrowerOffer.tick = tick1;
        lenderOffer.maxUnits = units;
        lenderOffer.tick = tick2;

        deal(address(loanToken), lender, units.mulDivDown(price2, WAD));
        deal(address(loanToken), address(this), 1); // cover up to 1-wei rounding gap from mulDivUp on sell offer
        collateralize(obligation, borrower, units);
        collateralize(obligation, address(this), units);

        take(units, address(this), lenderOffer);
        take(units, address(this), borrowerOffer);

        assertEq(midnight.creditOf(id, address(this)), 0, "credit");
        assertEq(midnight.debtOf(id, address(this)), 0, "debt");
    }

    function testBuyPastMaturity(uint256 timestamp) public {
        timestamp = bound(timestamp, obligation.maturity + 1, type(uint32).max);
        vm.warp(timestamp);
        borrowerOffer.expiry = timestamp;
        borrowerOffer.maxUnits = 100;
        borrowerOffer.tick = MAX_TICK;
        deal(address(loanToken), lender, 100);
        collateralize(obligation, borrower, 100);

        vm.expectRevert("seller is liquidatable");
        take(100, lender, borrowerOffer);
    }

    function testSellPastMaturity(uint256 timestamp) public {
        timestamp = bound(timestamp, obligation.maturity + 1, type(uint32).max);
        vm.warp(timestamp);
        lenderOffer.expiry = timestamp;
        lenderOffer.maxUnits = 100;
        lenderOffer.tick = MAX_TICK;
        deal(address(loanToken), lender, 100);
        collateralize(obligation, borrower, 100);

        vm.expectRevert("seller is liquidatable");
        take(100, borrower, lenderOffer);
    }

    function testBuyUnhealthy(uint256 units, uint256 tick, uint256 collateralized) public {
        units = bound(units, 1, maxAssets);
        collateralized = bound(collateralized, 0, units / 2);
        tick = bound(tick, 0, MAX_TICK);
        borrowerOffer.maxUnits = units;
        borrowerOffer.tick = tick;
        uint256 price = TickLib.tickToPrice(tick);
        deal(address(loanToken), lender, units.mulDivUp(price, WAD));
        collateralize(obligation, borrower, collateralized);

        vm.expectRevert("seller is liquidatable");
        take(units, lender, borrowerOffer);
    }

    function testSellUnhealthy(uint256 units, uint256 tick, uint256 collateralized) public {
        units = bound(units, 1, maxAssets);
        collateralized = bound(collateralized, 0, units / 2);
        tick = bound(tick, 0, MAX_TICK);
        lenderOffer.maxUnits = units;
        lenderOffer.tick = tick;
        uint256 price = TickLib.tickToPrice(tick);
        deal(address(loanToken), lender, units.mulDivDown(price, WAD));
        collateralize(obligation, borrower, collateralized);

        vm.expectRevert("seller is liquidatable");
        take(units, borrower, lenderOffer);
    }

    function testSession() public {
        vm.prank(lender);
        midnight.shuffleSession(lender);

        vm.expectRevert("invalid session");
        take(100, borrower, lenderOffer);
    }

    function testTakeOfferNotStarted(uint256 start) public {
        start = bound(start, block.timestamp + 1, type(uint256).max);
        Offer memory badOffer = lenderOffer;
        badOffer.start = start;
        vm.expectRevert("offer not started");
        take(0, borrower, badOffer);
    }

    function testTakeOfferExpired(uint256 elapsed) public {
        elapsed = bound(elapsed, 1, type(uint64).max);
        vm.warp(lenderOffer.expiry + elapsed);
        vm.expectRevert("offer expired");
        take(0, borrower, lenderOffer);
    }

    function testTakeBuyerAndSellerSame(uint256 pkey) public {
        pkey = bound(pkey, 1, type(uint128).max);
        address taker = vm.addr(pkey);
        privateKey[taker] = pkey;
        lenderOffer.maker = taker;

        vm.expectRevert("buyer and seller cannot be the same");
        take(0, taker, lenderOffer);
    }

    // maxSellerAssets / maxBuyerAssets tests.

    function testMaxSellerAssetsRevert() public {
        uint256 units = 100e18;
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        lenderOffer.maxUnits = 0;
        lenderOffer.maxSellerAssets = 1;

        vm.expectRevert("consumed seller assets");
        take(units, borrower, lenderOffer);
    }

    function testMaxSellerAssetsPass(uint256 units) public {
        units = bound(units, 1, maxAssets);
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        lenderOffer.maxUnits = 0;
        lenderOffer.maxSellerAssets = type(uint256).max;

        (, uint256 sellerAssets,) = take(units, borrower, lenderOffer);

        assertTrue(sellerAssets > 0);
    }

    function testMaxBuyerAssetsRevert() public {
        uint256 units = 100e18;
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        borrowerOffer.maxUnits = 0;
        borrowerOffer.maxBuyerAssets = 1;

        vm.expectRevert("consumed buyer assets");
        take(units, lender, borrowerOffer);
    }

    function testMaxBuyerAssetsPass(uint256 units) public {
        units = bound(units, 1, maxAssets);
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        borrowerOffer.maxUnits = 0;
        borrowerOffer.maxBuyerAssets = type(uint256).max;

        (uint256 buyerAssets,,) = take(units, lender, borrowerOffer);

        assertTrue(buyerAssets > 0);
    }

    function testMaxSellerAssetsExact() public {
        uint256 units = 100e18;
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        uint256 expectedSellerAssets = units.mulDivDown(price, WAD);

        lenderOffer.maxUnits = 0;
        lenderOffer.maxSellerAssets = expectedSellerAssets;

        (, uint256 sellerAssets,) = take(units, borrower, lenderOffer);
        assertEq(sellerAssets, expectedSellerAssets);
    }

    function testMaxBuyerAssetsExact() public {
        uint256 units = 100e18;
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        uint256 expectedBuyerAssets = units.mulDivUp(price, WAD);

        borrowerOffer.maxUnits = 0;
        borrowerOffer.maxBuyerAssets = expectedBuyerAssets;

        (uint256 buyerAssets,,) = take(units, lender, borrowerOffer);
        assertEq(buyerAssets, expectedBuyerAssets);
    }

    function testMaxSellerAssetsZeroMeansNoLimit(uint256 units) public {
        units = bound(units, 1, maxAssets);
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        lenderOffer.maxSellerAssets = 0;

        take(units, borrower, lenderOffer);
    }

    function testMaxBuyerAssetsZeroMeansNoLimit(uint256 units) public {
        units = bound(units, 1, maxAssets);
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        borrowerOffer.maxBuyerAssets = 0;

        take(units, lender, borrowerOffer);
    }

    function testMultipleMaxRevert() public {
        uint256 units = 100e18;
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        lenderOffer.maxSellerAssets = 1e18;
        lenderOffer.maxBuyerAssets = 1e18;
        lenderOffer.maxUnits = 0;

        vm.expectRevert("multiple max");
        take(units, borrower, lenderOffer);
    }

    function testMultipleMaxRevertUnitsAndSeller() public {
        uint256 units = 100e18;
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        lenderOffer.maxSellerAssets = 1e18;
        lenderOffer.maxUnits = 1e18;

        vm.expectRevert("multiple max");
        take(units, borrower, lenderOffer);
    }

    function testMultipleMaxRevertAllThree() public {
        uint256 units = 100e18;
        deal(address(loanToken), lender, units);
        collateralize(obligation, borrower, units);

        lenderOffer.maxSellerAssets = 1e18;
        lenderOffer.maxBuyerAssets = 1e18;
        lenderOffer.maxUnits = 1e18;

        vm.expectRevert("multiple max");
        take(units, borrower, lenderOffer);
    }

    // test tree / signatures.

    function testTakeWrongRoot() public {
        vm.expectRevert("invalid signature");
        vm.prank(borrower);
        midnight.take(
            100,
            borrower,
            address(0),
            hex"",
            borrower,
            lenderOffer,
            sig([borrowerOffer]),
            root([lenderOffer]),
            proof([lenderOffer])
        );
    }

    function testTakeInvalidSignature() public {
        vm.expectRevert("invalid signature");
        vm.prank(borrower);
        midnight.take(
            100,
            borrower,
            address(0),
            hex"",
            borrower,
            lenderOffer,
            Signature({v: 0, r: 0, s: 0}),
            root([lenderOffer]),
            proof([lenderOffer])
        );
    }

    function testTakeInvalidProofOneLeaf(bytes32[] memory proof) public {
        vm.assume(proof.length >= 1);
        vm.expectRevert("invalid proof");
        vm.prank(borrower);
        midnight.take(
            100, borrower, address(0), hex"", borrower, lenderOffer, sig([lenderOffer]), root([lenderOffer]), proof
        );
    }

    function testTakeInvalidProofTwoLeaves(Offer memory otherOffer, bytes32[] memory proof) public {
        vm.assume(proof.length >= 1);
        vm.assume(proof[0] != keccak256(abi.encode(otherOffer)));
        vm.expectRevert("invalid proof");
        vm.prank(borrower);
        midnight.take(
            100,
            borrower,
            address(0),
            hex"",
            borrower,
            lenderOffer,
            sig([lenderOffer, otherOffer]),
            root([lenderOffer, otherOffer]),
            proof
        );
    }

    function testTakeTwoLeaves(uint256 units, Offer memory otherOffer) public {
        units = bound(units, 0, maxAssets);
        uint256 price = TickLib.tickToPrice(lenderOffer.tick);
        deal(address(loanToken), lender, units.mulDivDown(price, WAD));
        collateralize(obligation, borrower, units);
        lenderOffer.maxUnits = units;

        vm.prank(borrower);
        midnight.take(
            units,
            borrower,
            address(0),
            hex"",
            borrower,
            lenderOffer,
            sig([lenderOffer, otherOffer]),
            root([lenderOffer, otherOffer]),
            proof([lenderOffer, otherOffer])
        );
    }

    // test callbacks.

    function testBuySellerCallback(uint256 units) public {
        units = bound(units, 0, maxAssets);
        uint256 collateral = units.mulDivUp(WAD, obligation.collateralParams[0].lltv);
        borrowerOffer.callback = address(new BorrowCallback());
        borrowerOffer.callbackData = abi.encode(0, collateral);
        borrowerOffer.maxUnits = units;
        borrowerOffer.tick = MAX_TICK;
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        deal(address(loanToken), lender, units.mulDivUp(price, WAD));
        deal(obligation.collateralParams[0].token, borrowerOffer.callback, collateral);
        assertEq(midnight.collateral(id, borrower, 0), 0);

        authorize(borrower, borrowerOffer.callback);

        take(units, lender, borrowerOffer);

        assertEq(midnight.collateral(id, borrower, 0), collateral);
        assertEq(BorrowCallback(borrowerOffer.callback).recordedData(), borrowerOffer.callbackData);
    }

    function testSellSellerCallback(uint256 units) public {
        units = bound(units, 0, maxAssets);
        uint256 collateral = units.mulDivUp(WAD, obligation.collateralParams[0].lltv);
        lenderOffer.maxUnits = units;
        lenderOffer.tick = MAX_TICK;
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        address callback = address(new BorrowCallback());
        deal(address(loanToken), lender, units.mulDivDown(price, WAD));
        deal(obligation.collateralParams[0].token, callback, collateral);

        authorize(borrower, callback);

        vm.prank(borrower);
        midnight.take(
            units,
            borrower,
            callback,
            abi.encode(0, collateral),
            borrower,
            lenderOffer,
            sig([lenderOffer]),
            root([lenderOffer]),
            proof([lenderOffer])
        );
        assertEq(midnight.collateral(id, borrower, 0), collateral);
        assertEq(BorrowCallback(callback).recordedData(), abi.encode(0, collateral));
    }

    function testSellSellerCallbackLiquidateRevertsWhileLiquidationLocked() public {
        uint256 units = 100e18;
        uint256 repaidUnits = 1e18;
        uint256 collateral = units.mulDivUp(WAD, obligation.collateralParams[0].lltv);
        lenderOffer.maxUnits = units;
        lenderOffer.tick = MAX_TICK;
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        ReentrantLiquidateBorrowCallback callback = new ReentrantLiquidateBorrowCallback();
        deal(address(loanToken), lender, units.mulDivDown(price, WAD));
        deal(obligation.collateralParams[0].token, address(callback), collateral);
        deal(address(loanToken), address(callback), repaidUnits);

        authorize(borrower, address(callback));

        vm.prank(borrower);
        midnight.take(
            units,
            borrower,
            address(callback),
            abi.encode(0, collateral, repaidUnits),
            borrower,
            lenderOffer,
            sig([lenderOffer]),
            root([lenderOffer]),
            proof([lenderOffer])
        );

        assertFalse(callback.liquidateSucceeded());
        assertEq(callback.liquidateError(), "liquidation locked");
        assertEq(midnight.debtOf(id, borrower), units);
        assertEq(midnight.collateral(id, borrower, 0), collateral);
    }

    // Show the effect of the wasLocked variable in `take`.
    // The variable is not necessary but makes the behavior easy to describe.
    // With wasLocked, a nested take does not restore liquidatability.
    function testSellNestedTakeLiquidateRevertsWhileLiquidationLocked() public {
        uint256 units = 100e18;
        uint256 repaidUnits = 1e18;
        uint256 collateral = units.mulDivUp(WAD, obligation.collateralParams[0].lltv);
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        lenderOffer.maxUnits = 2 * units;
        lenderOffer.tick = MAX_TICK;

        NestedTakeReentrantLiquidateCallback callback = new NestedTakeReentrantLiquidateCallback();
        deal(address(loanToken), lender, (2 * units).mulDivDown(price, WAD));
        deal(obligation.collateralParams[0].token, address(callback), 2 * collateral);
        deal(address(loanToken), address(callback), repaidUnits);

        authorize(borrower, address(callback));

        callback.prepare(
            lenderOffer,
            sig([lenderOffer]),
            root([lenderOffer]),
            proof([lenderOffer]),
            units,
            0,
            2 * collateral,
            repaidUnits
        );

        vm.prank(borrower);
        midnight.take(
            units,
            borrower,
            address(callback),
            "",
            borrower,
            lenderOffer,
            sig([lenderOffer]),
            root([lenderOffer]),
            proof([lenderOffer])
        );

        assertTrue(callback.reentered());
        assertFalse(callback.liquidateSucceeded());
        assertEq(callback.liquidateError(), "liquidation locked");
        assertTrue(midnight.liquidationLocked(id, borrower) == false);
        assertEq(midnight.debtOf(id, borrower), 2 * units);
        assertEq(midnight.collateral(id, borrower, 0), 2 * collateral);
    }

    function testSellSellerCallbackRevertsOnInvalidReturn(uint256 units) public {
        units = bound(units, 1, maxAssets);
        lenderOffer.maxUnits = units;
        lenderOffer.tick = MAX_TICK;
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        deal(address(loanToken), lender, units.mulDivDown(price, WAD));
        collateralize(obligation, borrower, units);
        address callback = address(new InvalidSellCallback());

        vm.expectRevert("invalid callback");
        vm.prank(borrower);
        midnight.take(
            units,
            borrower,
            callback,
            hex"",
            borrower,
            lenderOffer,
            sig([lenderOffer]),
            root([lenderOffer]),
            proof([lenderOffer])
        );
    }

    function testSellBuyerCallback(uint256 units) public {
        units = bound(units, 0, maxAssets);
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        uint256 assets = units.mulDivDown(price, WAD);
        lenderOffer.callback = address(new LendCallback());
        lenderOffer.callbackData = abi.encode(loanToken, assets);
        lenderOffer.maker = address(otherLender);
        lenderOffer.maxUnits = units;
        lenderOffer.tick = MAX_TICK;
        deal(address(loanToken), lenderOffer.callback, assets);
        collateralize(obligation, borrower, units);

        take(units, borrower, lenderOffer);

        assertEq(LendCallback(lenderOffer.callback).recordedData(), lenderOffer.callbackData);
    }

    function testBuyBuyerCallback(uint256 units) public {
        units = bound(units, 0, maxAssets);
        uint256 price = TickLib.tickToPrice(MAX_TICK);
        uint256 assets = units.mulDivUp(price, WAD);
        (address _otherLender,) = makeAddrAndKey("otherLender");
        address callback = address(new LendCallback());
        borrowerOffer.maxUnits = units;
        borrowerOffer.tick = MAX_TICK;
        deal(address(loanToken), callback, assets);
        collateralize(obligation, borrower, units);

        vm.prank(_otherLender);
        midnight.take(
            units,
            _otherLender,
            callback,
            abi.encode(address(loanToken), assets),
            address(0),
            borrowerOffer,
            sig([borrowerOffer]),
            root([borrowerOffer]),
            proof([borrowerOffer])
        );
        assertEq(LendCallback(callback).recordedData(), abi.encode(address(loanToken), assets));
    }

    // Summary of zero price tests:
    //
    // Trading at 0 succeeds in those cases:
    // - any offer / unit take input / 0 trading fee.
    // - sell offer / unit take input / > 0 trading fee.
    //
    // Otherwise it fails:
    // - by underflow when the trading fee is > 0, and the offer is a buy offer.

    // fee=0, sell, units
    function testPriceZero_NoTradingFee_sell() public {
        uint256 units = 1e18;
        borrowerOffer.tick = 0;
        borrowerOffer.maxUnits = units;
        collateralize(obligation, borrower, units);
        (uint256 buyerAssets, uint256 sellerAssets,) = take(units, lender, borrowerOffer);
        assertEq(buyerAssets, 0, "buyerAssets");
        assertEq(sellerAssets, 0, "sellerAssets");
        assertEq(midnight.creditOf(id, lender), units, "creditOf");
        assertEq(midnight.debtOf(id, borrower), units, "debtOf");
    }

    // fee>0, buy, units
    function testPriceZero_WithTradingFee_buy() public {
        midnight.touchObligation(obligation);
        midnight.setObligationTradingFee(id, 1, 1e12);
        uint256 units = 1e18;
        lenderOffer.tick = 0;
        lenderOffer.maxUnits = units;
        collateralize(obligation, borrower, units);
        vm.expectRevert();
        take(units, borrower, lenderOffer);
    }

    // fee>0, sell, units
    function testPriceZero_WithTradingFee_sell() public {
        midnight.touchObligation(obligation);
        midnight.setObligationTradingFee(id, 1, 1e12);
        uint256 fee = midnight.tradingFee(id, obligation.maturity - block.timestamp);
        uint256 units = 1e18;
        borrowerOffer.tick = 0;
        borrowerOffer.maxUnits = units;
        uint256 expectedBuyerAssets = units.mulDivUp(fee, WAD);
        deal(address(loanToken), lender, expectedBuyerAssets);
        collateralize(obligation, borrower, units);
        (uint256 buyerAssets, uint256 sellerAssets,) = take(units, lender, borrowerOffer);
        assertEq(buyerAssets, expectedBuyerAssets, "buyerAssets");
        assertEq(sellerAssets, 0, "sellerAssets");
        assertEq(midnight.creditOf(id, lender), units, "creditOf");
        assertEq(midnight.debtOf(id, borrower), units, "debtOf");
    }
}

contract BorrowCallback is ICallbacks {
    bytes public recordedData;
    bytes32 public recordedId;

    function onSell(bytes32 id, Obligation memory obligation, address seller, uint256, uint256, bytes memory data)
        external
        returns (bytes32)
    {
        require(id == IdLib.toId(obligation, block.chainid, msg.sender), "wrong id");
        recordedId = id;
        recordedData = data;
        (uint256 collateralIndex, uint256 amount) = abi.decode(data, (uint256, uint256));
        address collateralToken = obligation.collateralParams[collateralIndex].token;
        ERC20(collateralToken).approve(msg.sender, amount);
        Midnight(msg.sender).supplyCollateral(obligation, collateralIndex, amount, seller);
        return CALLBACK_SUCCESS;
    }

    function onBuy(bytes32, Obligation memory, address, uint256, uint256, bytes memory)
        external
        pure
        returns (bytes32)
    {
        return CALLBACK_SUCCESS;
    }

    function onLiquidate(bytes32, Obligation memory, uint256, uint256, uint256, address, bytes memory) external {}

    function onRepay(bytes32, Obligation memory, uint256, address, bytes memory) external {}
}

contract ReentrantLiquidateBorrowCallback is ICallbacks {
    bool public liquidateSucceeded;
    string public liquidateError;
    bytes public liquidateRevertData;

    function onSell(bytes32 id, Obligation memory obligation, address seller, uint256, uint256, bytes memory data)
        external
        returns (bytes32)
    {
        require(id == IdLib.toId(obligation, block.chainid, msg.sender), "wrong id");
        (uint256 collateralIndex, uint256 collateralAmount, uint256 repaidUnits) =
            abi.decode(data, (uint256, uint256, uint256));
        address collateralToken = obligation.collateralParams[collateralIndex].token;
        ERC20(collateralToken).approve(msg.sender, collateralAmount);
        Midnight(msg.sender).supplyCollateral(obligation, collateralIndex, collateralAmount, seller);

        Oracle oracle = Oracle(obligation.collateralParams[collateralIndex].oracle);
        uint256 healthyPrice = oracle.price();
        oracle.setPrice(healthyPrice / 2);
        ERC20(obligation.loanToken).approve(msg.sender, repaidUnits);
        try Midnight(msg.sender).liquidate(obligation, collateralIndex, 0, repaidUnits, seller, "") returns (
            uint256, uint256
        ) {
            liquidateSucceeded = true;
        } catch Error(string memory reason) {
            liquidateError = reason;
        } catch (bytes memory revertData) {
            liquidateRevertData = revertData;
        }
        oracle.setPrice(healthyPrice);
        return CALLBACK_SUCCESS;
    }

    function onBuy(bytes32, Obligation memory, address, uint256, uint256, bytes memory)
        external
        pure
        returns (bytes32)
    {
        return CALLBACK_SUCCESS;
    }

    function onLiquidate(bytes32, Obligation memory, uint256, uint256, uint256, address, bytes memory) external {}

    function onRepay(bytes32, Obligation memory, uint256, address, bytes memory) external {}
}

contract NestedTakeReentrantLiquidateCallback is ICallbacks {
    bool public reentered;
    bool public liquidateSucceeded;
    string public liquidateError;

    Offer internal storedOffer;
    Signature internal storedSig;
    bytes32 internal storedRoot;
    bytes32[] internal storedProof;
    uint256 internal innerUnits;
    uint256 internal storedCollateralIndex;
    uint256 internal storedCollateralAmount;
    uint256 internal storedRepaidUnits;

    function prepare(
        Offer memory _offer,
        Signature memory _sig,
        bytes32 _root,
        bytes32[] memory _proof,
        uint256 _innerUnits,
        uint256 _collateralIndex,
        uint256 _collateralAmount,
        uint256 _repaidUnits
    ) external {
        storedOffer = _offer;
        storedSig = _sig;
        storedRoot = _root;
        storedProof = _proof;
        innerUnits = _innerUnits;
        storedCollateralIndex = _collateralIndex;
        storedCollateralAmount = _collateralAmount;
        storedRepaidUnits = _repaidUnits;
    }

    function onSell(bytes32 id, Obligation memory obligation, address seller, uint256, uint256, bytes memory)
        external
        returns (bytes32)
    {
        require(id == IdLib.toId(obligation, block.chainid, msg.sender), "wrong id");
        if (!reentered) {
            uint256 idx = storedCollateralIndex;
            address collateralToken = obligation.collateralParams[idx].token;
            ERC20(collateralToken).approve(msg.sender, storedCollateralAmount);
            Midnight(msg.sender).supplyCollateral(obligation, idx, storedCollateralAmount, seller);

            reentered = true;
            Offer memory nestedOffer = storedOffer;
            Signature memory nestedSig = storedSig;
            bytes32[] memory nestedProof = storedProof;
            Midnight(msg.sender)
                .take(innerUnits, seller, address(this), "", seller, nestedOffer, nestedSig, storedRoot, nestedProof);

            Oracle oracle = Oracle(obligation.collateralParams[idx].oracle);
            uint256 healthyPrice = oracle.price();
            oracle.setPrice(healthyPrice / 2);
            ERC20(obligation.loanToken).approve(msg.sender, storedRepaidUnits);
            try Midnight(msg.sender).liquidate(obligation, idx, 0, storedRepaidUnits, seller, "") returns (
                uint256, uint256
            ) {
                liquidateSucceeded = true;
            } catch Error(string memory reason) {
                liquidateError = reason;
            }
            oracle.setPrice(healthyPrice);
        }
        return CALLBACK_SUCCESS;
    }

    function onBuy(bytes32, Obligation memory, address, uint256, uint256, bytes memory)
        external
        pure
        returns (bytes32)
    {
        return CALLBACK_SUCCESS;
    }

    function onLiquidate(bytes32, Obligation memory, uint256, uint256, uint256, address, bytes memory) external {}

    function onRepay(bytes32, Obligation memory, uint256, address, bytes memory) external {}
}

contract LendCallback is ICallbacks {
    bytes public recordedData;

    bytes32 public recordedId;

    function onBuy(bytes32 id, Obligation memory obligation, address, uint256 buyerAssets, uint256, bytes memory data)
        external
        returns (bytes32)
    {
        require(id == IdLib.toId(obligation, block.chainid, msg.sender), "wrong id");
        recordedId = id;
        recordedData = data;
        ERC20(obligation.loanToken).approve(msg.sender, buyerAssets);
        return CALLBACK_SUCCESS;
    }

    function onSell(bytes32, Obligation memory, address, uint256, uint256, bytes memory)
        external
        pure
        returns (bytes32)
    {
        return CALLBACK_SUCCESS;
    }

    function onLiquidate(bytes32, Obligation memory, uint256, uint256, uint256, address, bytes memory) external {}

    function onRepay(bytes32, Obligation memory, uint256, address, bytes memory) external {}
}

contract InvalidSellCallback is ICallbacks {
    function onBuy(bytes32, Obligation memory, address, uint256, uint256, bytes memory)
        external
        pure
        returns (bytes32)
    {
        return CALLBACK_SUCCESS;
    }

    function onSell(bytes32, Obligation memory, address, uint256, uint256, bytes memory)
        external
        pure
        returns (bytes32)
    {
        return bytes32(0);
    }

    function onLiquidate(bytes32, Obligation memory, uint256, uint256, uint256, address, bytes memory) external {}

    function onRepay(bytes32, Obligation memory, uint256, address, bytes memory) external {}
}
