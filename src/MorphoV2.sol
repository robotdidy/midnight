// SPDX-License-Identifier: BUSL-1.1
// Copyright (c) 2025 Morpho Association
pragma solidity 0.8.31;

import {UtilsLib} from "./libraries/UtilsLib.sol";
import {SafeTransferLib} from "./libraries/SafeTransferLib.sol";
import {
    WAD,
    ORACLE_PRICE_SCALE,
    FEE_STEP,
    MAX_FEE,
    MAX_LIF,
    TIME_TO_MAX_LIF,
    EIP712_DOMAIN_TYPEHASH,
    ROOT_TYPEHASH,
    OBLIGATION_DEPLOYER_PREFIX
} from "./libraries/ConstantsLib.sol";
import {IOracle} from "./interfaces/IOracle.sol";
import {
    IMorphoV2,
    Obligation,
    Offer,
    Signature,
    Collateral,
    Seizure,
    ObligationState
} from "./interfaces/IMorphoV2.sol";
import {ICallbacks, IFlashLoanCallback} from "./interfaces/ICallbacks.sol";
import {EventsLib} from "./libraries/EventsLib.sol";

/// OBLIGATIONS
/// @dev Obligations' collaterals must be sorted by token address.
contract MorphoV2 is IMorphoV2 {
    using UtilsLib for uint256;

    /// STORAGE ///

    mapping(bytes32 id => mapping(address user => uint256)) public sharesOf;
    mapping(bytes32 id => mapping(address user => uint256)) public debtOf;
    mapping(bytes32 id => mapping(address user => mapping(address collateralToken => uint256))) public collateralOf;
    mapping(bytes32 id => ObligationState) public obligationState;
    /// @dev Address of a contract whose bytecode is abi.encode(obligation)
    mapping(bytes32 id => address) internal idToObligationContract;

    /// @dev Groups are useful to have a global offered amount shared accross multiple offers ("OCO").
    /// @dev To work as expected, all offers in a same group should have the same assets, obligationUnits,
    /// obligationShares and loan token.
    mapping(address user => mapping(bytes32 group => uint256)) public consumed;

    /// @dev Offers should have the current session to be valid.
    /// @dev The session can be shuffled by the user to cancel all current offers easily and efficiently.
    mapping(address user => bytes32) public session;

    /// @dev Default fees per loan token. Set when the obligation is created. Can be later decreased by the feeSetter.
    mapping(address loanToken => uint16[6]) public defaultFees;

    address public tradingFeeRecipient;

    /// @dev Contract owner for administrative functions.
    address public owner;

    /// @dev Address that can set trading fees.
    address public feeSetter;

    /// CONSTRUCTOR ///

    constructor() {
        owner = msg.sender;
        emit EventsLib.Constructor(owner);
    }

    /// MULTICALL ///

    function multicall(bytes[] calldata calls) external {
        for (uint256 i = 0; i < calls.length; i++) {
            (bool success, bytes memory returnData) = address(this).delegatecall(calls[i]);
            if (!success) {
                assembly ("memory-safe") {
                    revert(add(returnData, 0x20), mload(returnData))
                }
            }
        }
    }

    /// ADMIN FUNCTIONS ///

    function setOwner(address newOwner) external {
        require(msg.sender == owner, "Only owner");
        owner = newOwner;
        emit EventsLib.SetOwner(newOwner);
    }

    function setFeeSetter(address newFeeSetter) external {
        require(msg.sender == owner, "Only owner");
        feeSetter = newFeeSetter;
        emit EventsLib.SetFeeSetter(newFeeSetter);
    }

    /// @dev Overrides the fee of a specific obligation.
    function setObligationTradingFee(bytes32 id, uint256 index, uint256 newTradingFee) external {
        require(msg.sender == feeSetter, "Only feeSetter");
        require(index <= 5, "Invalid index");
        require(newTradingFee % FEE_STEP == 0, "fee should be a multiple of FEE_STEP");
        require(
            newTradingFee <= uint256(obligationState[id].fees[index]) * FEE_STEP,
            "New trading fee is higher than current"
        );
        // forge-lint: disable-next-line(unsafe-typecast) as newTradingFee is less than MAX_FEE
        obligationState[id].fees[index] = uint16(newTradingFee / FEE_STEP);
        emit EventsLib.SetObligationTradingFee(id, index, newTradingFee);
    }

    /// @dev Doesn't change the fee of already created obligations.
    function setDefaultTradingFee(address loanToken, uint256 index, uint256 newTradingFee) external {
        require(msg.sender == feeSetter, "Only feeSetter");
        require(newTradingFee <= MAX_FEE, "Trading fee too high");
        require(index <= 5, "Invalid index");
        require(newTradingFee % FEE_STEP == 0, "fee should be a multiple of FEE_STEP");
        // forge-lint: disable-next-line(unsafe-typecast) as newTradingFee is less than MAX_FEE
        defaultFees[loanToken][index] = uint16(newTradingFee / FEE_STEP);
        emit EventsLib.SetDefaultTradingFee(loanToken, index, newTradingFee);
    }

    function setTradingFeeRecipient(address recipient) external {
        require(msg.sender == owner, "Only owner");
        tradingFeeRecipient = recipient;
        emit EventsLib.SetTradingFeeRecipient(recipient);
    }

    /// ENTRY-POINTS ///

    /// @dev Returns buyerAssets, sellerAssets, obligationUnits, obligationShares.
    /// @dev Same function used to buy and sell.
    /// @dev If one wants to match two offers without taking a position, they can batch take them and not have a
    /// position at the end.
    /// @dev Neither the taker nor the maker can pass from having shares to having debt in one take.
    function take(
        uint256 buyerAssets,
        uint256 sellerAssets,
        uint256 obligationUnits,
        uint256 obligationShares,
        address taker,
        Offer memory offer,
        Signature memory sig,
        bytes32 root,
        bytes32[] memory proof,
        address takerCallback,
        bytes memory takerCallbackData
    ) public returns (uint256, uint256, uint256, uint256) {
        require(
            UtilsLib.atMostOneNonZero(buyerAssets, sellerAssets, obligationUnits, obligationShares),
            "inconsistent input"
        );
        require(
            UtilsLib.atMostOneNonZero(offer.assets, offer.obligationUnits, offer.obligationShares),
            "inconsistent offer input"
        );
        require(block.timestamp >= offer.start, "offer not started");
        require(block.timestamp <= offer.expiry, "offer expired");
        require(offer.maker != taker, "buyer and seller cannot be the same");
        require(signer(root, sig) == offer.maker, "invalid signature");
        require(UtilsLib.isLeaf(root, keccak256(abi.encode(offer)), proof), "invalid proof");
        require(offer.session == session[offer.maker], "invalid session");
        bytes32 id = touchObligation(offer.obligation);
        ObligationState storage _obligationState = obligationState[id];

        (
            address buyer,
            address buyerCallback,
            bytes memory buyerCallbackData,
            address seller,
            address sellerCallback,
            bytes memory sellerCallbackData
        ) = offer.buy
            ? (offer.maker, offer.callback, offer.callbackData, taker, takerCallback, takerCallbackData)
            : (taker, takerCallback, takerCallbackData, offer.maker, offer.callback, offer.callbackData);

        uint256 timeToMaturity = UtilsLib.zeroFloorSub(offer.obligation.maturity, block.timestamp);
        uint256 _tradingFee = tradingFee(id, timeToMaturity);
        uint256 sellerPrice = offer.buy ? offer.price - _tradingFee : offer.price;
        uint256 buyerPrice = sellerPrice + _tradingFee;
        require(buyerPrice <= WAD, "cannot trade at price above one");

        if (buyerAssets > 0) {
            obligationUnits = buyerAssets.mulDivDown(WAD, buyerPrice);
            sellerAssets = buyerAssets.mulDivDown(sellerPrice, buyerPrice);
            obligationShares =
                obligationUnits.mulDivDown(_obligationState.totalShares + 1, _obligationState.totalUnits + 1);
        } else if (sellerAssets > 0) {
            obligationUnits = sellerAssets.mulDivDown(WAD, sellerPrice);
            buyerAssets = sellerAssets.mulDivDown(buyerPrice, sellerPrice);
            obligationShares =
                obligationUnits.mulDivDown(_obligationState.totalShares + 1, _obligationState.totalUnits + 1);
        } else if (obligationUnits > 0) {
            buyerAssets = obligationUnits.mulDivDown(buyerPrice, WAD);
            sellerAssets = obligationUnits.mulDivDown(sellerPrice, WAD);
            obligationShares =
                obligationUnits.mulDivDown(_obligationState.totalShares + 1, _obligationState.totalUnits + 1);
        } else {
            obligationUnits =
                obligationShares.mulDivDown(_obligationState.totalUnits + 1, _obligationState.totalShares + 1);
            buyerAssets = obligationUnits.mulDivDown(buyerPrice, WAD);
            sellerAssets = obligationUnits.mulDivDown(sellerPrice, WAD);
        }

        if (offer.assets > 0) {
            require(
                (consumed[offer.maker][offer.group] += offer.buy ? buyerAssets : sellerAssets) <= offer.assets,
                "consumed"
            );
        } else if (offer.obligationUnits > 0) {
            require((consumed[offer.maker][offer.group] += obligationUnits) <= offer.obligationUnits, "consumed");
        } else {
            require((consumed[offer.maker][offer.group] += obligationShares) <= offer.obligationShares, "consumed");
        }

        bool buyerIsLender = (debtOf[id][buyer] == 0);
        bool sellerIsBorrower = (sharesOf[id][seller] == 0);
        if (buyerIsLender && sellerIsBorrower) {
            // Lender enters + borrower enters.
            sharesOf[id][buyer] += obligationShares;
            debtOf[id][seller] += obligationUnits;
            _obligationState.totalShares += UtilsLib.toUint128(obligationShares);
            _obligationState.totalUnits += UtilsLib.toUint128(obligationUnits);
        } else if (buyerIsLender && !sellerIsBorrower) {
            // Lender enters + lender exits.
            sharesOf[id][buyer] += obligationShares;
            sharesOf[id][seller] -= obligationShares;
        } else if (!buyerIsLender && sellerIsBorrower) {
            // Borrower exits + borrower enters.
            debtOf[id][buyer] -= obligationUnits;
            debtOf[id][seller] += obligationUnits;
        } else {
            // Borrower exits + lender exits.
            debtOf[id][buyer] -= obligationUnits;
            sharesOf[id][seller] -= obligationShares;
            _obligationState.totalShares -= UtilsLib.toUint128(obligationShares);
            _obligationState.totalUnits -= UtilsLib.toUint128(obligationUnits);
        }

        emit EventsLib.Take(
            msg.sender,
            id,
            buyerAssets,
            sellerAssets,
            obligationUnits,
            obligationShares,
            taker,
            buyerIsLender,
            sellerIsBorrower
        );

        if (buyerCallback != address(0)) {
            ICallbacks(buyerCallback)
                .onBuy(
                    offer.obligation,
                    buyer,
                    buyerAssets,
                    sellerAssets,
                    obligationUnits,
                    obligationShares,
                    buyerCallbackData
                );
        }

        SafeTransferLib.safeTransferFrom(
            offer.obligation.loanToken, buyer, tradingFeeRecipient, buyerAssets - sellerAssets
        );
        SafeTransferLib.safeTransferFrom(offer.obligation.loanToken, buyer, seller, sellerAssets);

        if (sellerCallback != address(0)) {
            ICallbacks(sellerCallback)
                .onSell(
                    offer.obligation,
                    seller,
                    buyerAssets,
                    sellerAssets,
                    obligationUnits,
                    obligationShares,
                    sellerCallbackData
                );
        }

        require(isHealthy(offer.obligation, seller), "Seller is unhealthy");

        return (buyerAssets, sellerAssets, obligationUnits, obligationShares);
    }

    /// @dev Will revert if there is no withdrawable funds.
    function withdraw(Obligation memory obligation, uint256 obligationUnits, uint256 shares, address onBehalf)
        external
        returns (uint256, uint256)
    {
        require(UtilsLib.atMostOneNonZero(obligationUnits, shares), "INCONSISTENT_INPUT");
        bytes32 id = touchObligation(obligation);
        ObligationState storage _obligationState = obligationState[id];

        if (obligationUnits > 0) {
            shares = obligationUnits.mulDivUp(_obligationState.totalShares + 1, _obligationState.totalUnits + 1);
        } else {
            obligationUnits = shares.mulDivDown(_obligationState.totalUnits + 1, _obligationState.totalShares + 1);
        }

        sharesOf[id][onBehalf] -= shares;
        _obligationState.withdrawable -= obligationUnits;
        _obligationState.totalShares -= UtilsLib.toUint128(shares);
        _obligationState.totalUnits -= UtilsLib.toUint128(obligationUnits);

        emit EventsLib.Withdraw(msg.sender, id, obligationUnits, shares, onBehalf);

        SafeTransferLib.safeTransfer(obligation.loanToken, msg.sender, obligationUnits);

        return (obligationUnits, shares);
    }

    function repay(Obligation memory obligation, uint256 obligationUnits, address onBehalf) external {
        bytes32 id = touchObligation(obligation);

        debtOf[id][onBehalf] -= obligationUnits;
        obligationState[id].withdrawable += obligationUnits;

        emit EventsLib.Repay(msg.sender, id, obligationUnits, onBehalf);

        SafeTransferLib.safeTransferFrom(obligation.loanToken, msg.sender, address(this), obligationUnits);
    }

    function supplyCollateral(Obligation memory obligation, address collateral, uint256 assets, address onBehalf)
        external
    {
        bytes32 id = touchObligation(obligation);

        collateralOf[id][onBehalf][collateral] += assets;

        emit EventsLib.SupplyCollateral(msg.sender, id, collateral, assets, onBehalf);

        SafeTransferLib.safeTransferFrom(collateral, msg.sender, address(this), assets);
    }

    function withdrawCollateral(Obligation memory obligation, address collateral, uint256 assets, address onBehalf)
        external
    {
        bytes32 id = touchObligation(obligation);

        collateralOf[id][onBehalf][collateral] -= assets;

        require(isHealthy(obligation, onBehalf), "Unhealthy borrower");

        emit EventsLib.WithdrawCollateral(msg.sender, id, collateral, assets, onBehalf);

        SafeTransferLib.safeTransfer(collateral, msg.sender, assets);
    }

    /// @dev On each seizure at least one of `repaid` or `seized` should be equal to zero.
    /// @dev Accounts are liquidatable if they are unhealthy or if the maturity is reached.
    /// @dev If an account is healthy, the LIF grows linearly from 1 at maturity to MAX_LIF at maturity +
    /// TIME_TO_MAX_LIF.
    /// @param obligation The obligation.
    /// @param seizures An array of amounts of debt to repay or assets to seize with the index of the collateral in the
    /// obligation's collateral assets.
    /// @param borrower The debtor of the loan.
    /// @param data Arbitrary data to pass to the callback. Pass empty data if not needed.
    /// @return A collection of the actual amounts of debt repaid or asset seized with the collateral index.
    function liquidate(Obligation memory obligation, Seizure[] memory seizures, address borrower, bytes calldata data)
        external
        returns (Seizure[] memory)
    {
        uint256 repayableDebt;
        uint256 maxDebt;
        bytes32 id = touchObligation(obligation);
        ObligationState storage _obligationState = obligationState[id];
        uint256[] memory prices = new uint256[](obligation.collaterals.length);

        for (uint256 i = 0; i < obligation.collaterals.length; i++) {
            Collateral memory _collateral = obligation.collaterals[i];
            uint256 price = IOracle(_collateral.oracle).price();
            prices[i] = price;
            uint256 _collateralOf = collateralOf[id][borrower][_collateral.token];
            maxDebt += _collateralOf.mulDivDown(price, ORACLE_PRICE_SCALE).mulDivDown(_collateral.lltv, WAD);
            repayableDebt += _collateralOf.mulDivUp(WAD, MAX_LIF).mulDivUp(price, ORACLE_PRICE_SCALE);
        }

        uint256 originalDebt = debtOf[id][borrower];
        require(block.timestamp > obligation.maturity || originalDebt > maxDebt, "position is not liquidatable");

        uint256 lif = originalDebt > maxDebt
            ? MAX_LIF
            : UtilsLib.min(MAX_LIF, WAD + (MAX_LIF - WAD) * (block.timestamp - obligation.maturity) / TIME_TO_MAX_LIF);

        uint256 badDebt = originalDebt.zeroFloorSub(repayableDebt);
        if (badDebt > 0) {
            debtOf[id][borrower] -= badDebt;
            _obligationState.totalUnits -= UtilsLib.toUint128(badDebt);
        }

        uint256 totalRepaid;

        for (uint256 i = 0; i < seizures.length; i++) {
            Seizure memory seizure = seizures[i];
            require(UtilsLib.atMostOneNonZero(seizure.repaid, seizure.seized), "INCONSISTENT_INPUT");

            if (seizure.seized > 0) {
                seizure.repaid =
                    seizure.seized.mulDivUp(WAD, lif).mulDivUp(prices[seizure.collateralIndex], ORACLE_PRICE_SCALE);
            } else {
                seizure.seized =
                    seizure.repaid.mulDivDown(ORACLE_PRICE_SCALE, prices[seizure.collateralIndex]).mulDivDown(lif, WAD);
            }

            totalRepaid += seizure.repaid;
            address collateralToken = obligation.collaterals[seizure.collateralIndex].token;
            collateralOf[id][borrower][collateralToken] -= seizure.seized;
        }

        _obligationState.withdrawable += totalRepaid;
        debtOf[id][borrower] -= totalRepaid;

        emit EventsLib.Liquidate(msg.sender, id, seizures, borrower, totalRepaid, badDebt);

        for (uint256 i = 0; i < seizures.length; i++) {
            Seizure memory seizure = seizures[i];
            SafeTransferLib.safeTransfer(
                obligation.collaterals[seizure.collateralIndex].token, msg.sender, seizure.seized
            );
        }

        if (data.length > 0) ICallbacks(msg.sender).onLiquidate(seizures, borrower, msg.sender, data);

        SafeTransferLib.safeTransferFrom(obligation.loanToken, msg.sender, address(this), totalRepaid);

        return seizures;
    }

    function consume(bytes32 group, uint256 amount) external {
        consumed[msg.sender][group] += amount;

        emit EventsLib.Consume(msg.sender, group, amount);
    }

    /// @dev TODO: is it safe enough?
    function shuffleSession() external {
        bytes32 newSession = keccak256(abi.encode(session[msg.sender], blockhash(block.number - 1)));
        session[msg.sender] = newSession;

        emit EventsLib.ShuffleSession(msg.sender, newSession);
    }

    function flashLoan(address token, uint256 assets, address callback, bytes calldata data) external {
        emit EventsLib.FlashLoan(msg.sender, token, assets);

        SafeTransferLib.safeTransfer(token, msg.sender, assets);

        IFlashLoanCallback(callback).onFlashLoan(token, assets, data);

        SafeTransferLib.safeTransferFrom(token, msg.sender, address(this), assets);
    }

    /// @dev Returns the obligation id and creates the obligation if it doesn't exist yet.
    function touchObligation(Obligation memory obligation) public returns (bytes32) {
        bytes32 id = toId(obligation);
        if (!obligationState[id].created) {
            address previousCollateralToken;
            for (uint256 i = 0; i < obligation.collaterals.length; i++) {
                address collateralToken = obligation.collaterals[i].token;
                require(collateralToken > previousCollateralToken, "collaterals not sorted");
                previousCollateralToken = collateralToken;
            }

            obligationState[id].created = true;
            obligationState[id].fees = defaultFees[obligation.loanToken];

            bytes memory obligationData = abi.encode(obligation);
            bytes memory creationCode = abi.encodePacked(OBLIGATION_DEPLOYER_PREFIX, obligationData);
            address _idToObligationContract;
            assembly ("memory-safe") {
                _idToObligationContract := create(0, add(creationCode, 0x20), mload(creationCode))
            }
            require(_idToObligationContract != address(0), "obligation deploy failed");
            idToObligationContract[id] = _idToObligationContract;

            emit EventsLib.ObligationCreated(id, obligation, _idToObligationContract);
        }
        return id;
    }

    /// VIEW FUNCTIONS ///

    function idToObligation(bytes32 id) external view returns (Obligation memory) {
        address _idToObligationContract = idToObligationContract[id];
        if (_idToObligationContract == address(0)) {
            return Obligation(address(0), new Collateral[](0), 0);
        }
        uint256 size;
        assembly ("memory-safe") {
            size := extcodesize(_idToObligationContract)
        }
        bytes memory data = new bytes(size);
        assembly ("memory-safe") {
            extcodecopy(_idToObligationContract, add(data, 0x20), 0, size)
        }
        return abi.decode(data, (Obligation));
    }

    function totalUnits(bytes32 id) external view returns (uint256) {
        return obligationState[id].totalUnits;
    }

    function totalShares(bytes32 id) external view returns (uint256) {
        return obligationState[id].totalShares;
    }

    function obligationCreated(bytes32 id) external view returns (bool) {
        return obligationState[id].created;
    }

    function withdrawable(bytes32 id) external view returns (uint256) {
        return obligationState[id].withdrawable;
    }

    function fees(bytes32 id) external view returns (uint16[6] memory) {
        return obligationState[id].fees;
    }

    function toId(Obligation memory obligation) public view returns (bytes32) {
        return keccak256(abi.encode(block.chainid, address(this), obligation));
    }

    function isHealthy(Obligation memory obligation, address borrower) public view returns (bool) {
        bytes32 id = toId(obligation);
        uint256 debt = debtOf[id][borrower];
        uint256 maxDebt;
        for (uint256 i = 0; i < obligation.collaterals.length && maxDebt < debt; i++) {
            Collateral memory collateral = obligation.collaterals[i];
            uint256 price = IOracle(collateral.oracle).price();
            maxDebt += collateralOf[id][borrower][collateral.token].mulDivDown(price, ORACLE_PRICE_SCALE)
                .mulDivDown(collateral.lltv, WAD);
        }
        return maxDebt >= debt;
    }

    function domainSeparator() internal view returns (bytes32) {
        return keccak256(abi.encode(EIP712_DOMAIN_TYPEHASH, block.chainid, address(this)));
    }

    function signer(bytes32 root, Signature memory signature) internal view returns (address) {
        bytes32 structHash = keccak256(abi.encode(ROOT_TYPEHASH, root));
        bytes32 digest = keccak256(bytes.concat("\x19\x01", domainSeparator(), structHash));
        address tentativeSigner = ecrecover(digest, signature.v, signature.r, signature.s);
        require(tentativeSigner != address(0), "invalid signature");
        return tentativeSigner;
    }

    /// @dev Returns the trading fee using piecewise linear interpolation between breakpoints.
    function tradingFee(bytes32 id, uint256 timeToMaturity) public view returns (uint256) {
        uint16[6] memory _fees = obligationState[id].fees;

        if (timeToMaturity >= 180 days) return uint256(_fees[5]) * FEE_STEP;

        // forgefmt: disable-start
        (uint256 index, uint256 start, uint256 end) =
            timeToMaturity < 1 days  ? (0, 0 days, 1 days) :
            timeToMaturity < 7 days  ? (1, 1 days, 7 days) :
            timeToMaturity < 30 days ? (2, 7 days, 30 days) :
            timeToMaturity < 90 days ? (3, 30 days, 90 days) :
                                       (4, 90 days, 180 days);
        // forgefmt: disable-end

        uint256 feeLower = uint256(_fees[index]) * FEE_STEP;
        uint256 feeUpper = uint256(_fees[index + 1]) * FEE_STEP;

        return (feeLower * (end - timeToMaturity) + feeUpper * (timeToMaturity - start)) / (end - start);
    }
}
